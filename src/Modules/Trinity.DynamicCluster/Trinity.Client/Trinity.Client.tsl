struct RegisterClientRequest
{
	// The cookie generated by the client-side, as a simple authenticator.
	int Cookie;
}

struct RegisterClientResponse
{
	// The instance id, produced by the server-side memory cloud.
	int InstanceId;
	int PartitionCount;
}

struct PollEventsRequest
{
	int InstanceId;
	int Cookie;
}

struct PostSyncResponseRequest
{
	int InstanceId;
	int Cookie;
	List<byte> Result;
}

struct PollEventsResponse
{
	// null if no events
	optional List<byte> Result;
}

struct UnregisterClientRequest
{
	int InstanceId;
	int Cookie;
}

protocol RegisterClient
{
	Type: Syn;
	Request: RegisterClientRequest;
	Response: RegisterClientResponse;
}

protocol PollAsyncEvents
{
	Type: Syn;
	Request: PollEventsRequest;
	Response: PollEventsResponse;
}

protocol PollSyncEvents
{
	Type: Syn;
	Request: PollEventsRequest;
	Response: PollEventsResponse;
}

protocol PostSyncResponse
{
	Type: Syn;
	Request: PostSyncResponseRequest;
	Response: void;
}

protocol UnregisterClient
{
	Type: Syn;
	Request: UnregisterClientRequest;
	Response: void;
}

module TrinityClientModule
{
	protocol RegisterClient;
	protocol UnregisterClient;
	protocol PollSyncEvents;
	protocol PollAsyncEvents;
	protocol PostSyncResponse;
}